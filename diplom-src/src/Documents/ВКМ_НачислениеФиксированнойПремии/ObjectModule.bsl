
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	СформироватьДополнительныеНачисления();
	
	СформироватьУдержания();
	
	РассчитатьПроцентНДФЛ();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДополнительныеНачисления()
	
	ДобавитьЗаписиФиксированнойПремии();
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();	
	
КонецПроцедуры

Процедура СформироватьУдержания()
	
	ДобавитьЗаписиПроцентНДФЛ();
	
	Движения.ВКМ_Удержания.Записать();
	
КонецПроцедуры

Процедура ДобавитьЗаписиФиксированнойПремии()
	
	Для Каждого Строка Из ВКМ_СписокСотрудников Цикл
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ПериодРегистрации = ВКМ_ПериодРегистрации;
		Движение.ВКМ_Подразделение = ВКМ_Подразделение;
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.Премия;
		Движение.ПериодДействияНачало = НачалоМесяца(ВКМ_ПериодРегистрации);
		Движение.ПериодДействияКонец = КонецМесяца(ВКМ_ПериодРегистрации);
		Движение.ВКМ_Результат = Строка.ВКМ_СуммаПремии;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьЗаписиПроцентНДФЛ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников.ВКМ_Сотрудник,
	|	ВКМ_НачислениеФиксированнойПремии.ВКМ_Подразделение
	|ИЗ
	|	Документ.ВКМ_НачислениеФиксированнойПремии.ВКМ_СписокСотрудников КАК
	|		ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВКМ_НачислениеФиксированнойПремии КАК ВКМ_НачислениеФиксированнойПремии
	|		ПО ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников.Ссылка = ВКМ_НачислениеФиксированнойПремии.Ссылка
	|ГДЕ
	|	ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ВКМ_Удержания.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.ПроцентНДФЛ;
		Движение.ПериодРегистрации = ВКМ_ПериодРегистрации;
		Движение.БазовыйПериодНачало = НачалоМесяца(ВКМ_ПериодРегистрации);
		Движение.БазовыйПериодКонец = КонецМесяца(ВКМ_ПериодРегистрации);
		Движение.ПериодДействияНачало = НачалоМесяца(ВКМ_ПериодРегистрации);
		Движение.ПериодДействияКонец = КонецМесяца(ВКМ_ПериодРегистрации);
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьПроцентНДФЛ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|
	|	ВКМ_Удержания.НомерСтроки,
	|	ЕСТЬNULL(ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.ВКМ_РезультатБаза, 0) КАК БазаДоп,
	|	ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез
	|ПОМЕСТИТЬ ВТДанные
	|ИЗ
	|	РегистрРасчета.ВКМ_Удержания КАК ВКМ_Удержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ВКМ_Удержания.БазаВКМ_ДополнительныеНачисления(&Измерения, &Измерения, &Разрезы,
	|			Регистратор = &Ссылка) КАК ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления
	|		ПО ВКМ_Удержания.НомерСтроки = ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.НомерСтроки
	|ГДЕ
	|	ВКМ_Удержания.Регистратор = &Ссылка
	|	И ВКМ_УдержанияБазаВКМ_ДополнительныеНачисления.РегистраторРазрез = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДанные.НомерСтроки КАК НомерСтроки,
	|	СУММА(ВТДанные.БазаДоп) КАК БазаДоп
	|ИЗ
	|	ВТДанные КАК ВТДанные
	|СГРУППИРОВАТЬ ПО
	|	ВТДанные.НомерСтроки";
	
	Измерения = Новый Массив;
	Измерения.Добавить("ВКМ_Сотрудник");
	Измерения.Добавить("ВКМ_Подразделение");
	
	Разрезы = Новый Массив;
	Разрезы.Добавить("Регистратор");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("Разрезы", Разрезы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Строка = Движения.ВКМ_Удержания[Выборка.НомерСтроки - 1];
		
		База = Выборка.БазаДоп;
		Если База <> 0 Тогда
			Строка.ВКМ_Показатель = База;
			Строка.ВКМ_Результат = База * 13 / 100;
		КонецЕсли;
	КонецЦикла;
	
	Движения.ВКМ_Удержания.Записать(, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли