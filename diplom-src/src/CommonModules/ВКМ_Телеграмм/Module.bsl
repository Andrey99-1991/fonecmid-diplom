
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции

Процедура ОтправитьСообщениеВТелеграмм(ТекстСообщения, ВКМ_ТокенУправленияТелеграмБотом, ВКМ_ИдентификаторГруппыДляОповещения) Экспорт
	
	Соединение = Новый HTTPСоединение("api.telegram.org", 443, , , , , Новый ЗащищенноеСоединениеOpenSSL);  
	ДанныеДляОтправки = Новый структура;
	ДанныеДляОтправки.Вставить("chat_id", ВКМ_ИдентификаторГруппыДляОповещения);
	ДанныеДляОтправки.Вставить("text", ТекстСообщения);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	Токен = ВКМ_ТокенУправленияТелеграмБотом;
	ЗаписатьJSON(ЗаписьJSON, ДанныеДляОтправки);
	Строказапроса = ЗаписьJSON.Закрыть();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("content-type", "application/json");
	Запрос=Новый HTTPЗапрос("bot" + Токен + "/sendMessage", Заголовки);
	Запрос.УстановитьТелоИзСтроки(строказапроса);
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния <> 200 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		ЗаписьЖурналаРегистрации(ТелоОтвета, УровеньЖурналаРегистрации.Ошибка);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьЗначениеКонстанты(ИмяКонстанты) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Константы." + ИмяКонстанты +" КАК " + ИмяКонстанты + "
		|ИЗ
		|	Константы КАК Константы";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Возврат ВыборкаДетальныеЗаписи[ИмяКонстанты];	
	КонецЦикла;
	
КонецФункции	

Процедура ЗаписатьСообщениеВСправочник(ТекстСообщения, НаименованиеДокумента) Экспорт

	ОбъектСообщение = Справочники.ВКМ_УведомленияТелеграмБоту.СоздатьЭлемент();
	ОбъектСообщение.ВКМ_ТекстСообщения = ТекстСообщения;
	ОбъектСообщение.Наименование = НаименованиеДокумента;
	ОбъектСообщение.Записать();

КонецПроцедуры

Процедура ОтправитьСообщенияВТелеграммРегламентно() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УведомленияТелеграмБоту.Ссылка,
	|	УведомленияТелеграмБоту.ВКМ_ТекстСообщения КАК ТекстСообщения
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК УведомленияТелеграмБоту
	|ГДЕ
	|	УведомленияТелеграмБоту.ПометкаУдаления = ЛОЖЬ";

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ВКМ_ТокенУправленияТелеграмБотом = ПолучитьЗначениеКонстанты("ВКМ_ТокенУправленияТелеграмБотом");
	ВКМ_ИдентификаторГруппыДляОповещения = ПолучитьЗначениеКонстанты("ВКМ_ИдентификаторГруппыДляОповещения");
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстСообщения = ВыборкаДетальныеЗаписи.ТекстСообщения;
		ОтправитьСообщениеВТелеграмм(ТекстСообщения, ВКМ_ТокенУправленияТелеграмБотом, ВКМ_ИдентификаторГруппыДляОповещения);
		ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект().УстановитьПометкуУдаления(Истина);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
