#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СоздатьНаРеализации(Параметры) Экспорт
	
	СписокРеализации = РеализацияВЭтомМесяце(Параметры).Выгрузить();
	МассивДокументов = Новый Массив;
	Для Каждого Договор Из Параметры.МассивДоговоров Цикл	
		Отбор = Новый Структура();
		Отбор.Вставить("Договор", Договор);	
		Если СписокРеализации.НайтиСтроки(Отбор).Количество() = 0 Тогда
			ДокРеализация = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			ДокРеализация.Заполнить(Неопределено);
			ДокРеализация.Дата = ОбщегоНазначения.ТекущаяДатаПользователя();
			ДокРеализация.Договор = Договор;			
			ДокРеализация.ВыполнитьАвтозаполнение();
			НетОшибок = ДокРеализация.ПроверитьЗаполнение();
			Если НетОшибок Тогда
				ДокРеализация.Записать(РежимЗаписиДокумента.Проведение);
			Иначе
				ОбщегоНазначения.СообщитьПользователю("Ошибка записи документа. Не заполнены обязательные реквизиты ");
				Продолжить;
			КонецЕсли;
			МассивДокументов.Добавить(ДокРеализация.Ссылка)			
		КонецЕсли;
	КонецЦикла;
	
	Возврат МассивДокументов;
	
КонецФункции

Функция РеализацияВЭтомМесяце(Параметры)
	
	Период = Параметры.Период;
	МассивДоговоров = Параметры.МассивДоговоров;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Ссылка,
		|	РеализацияТоваровУслуг.Договор
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|ГДЕ
		|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И РеализацияТоваровУслуг.Договор В (&Договора)";
	
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Период));
	Запрос.УстановитьПараметр("ДатаОкончания",  КонецМесяца(Период));
	Запрос.УстановитьПараметр("Договора", МассивДоговоров);
	
	РезультатЗапроса = Запрос.Выполнить();
	
 	Возврат РезультатЗапроса;
	
КонецФункции	

#КонецОбласти

#КонецЕсли